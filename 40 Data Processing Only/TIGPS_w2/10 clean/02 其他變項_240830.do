* Last Updated: 2024. 08. 30
* File name: []
* Data: tigps_w2
* Subject: 


/*****************************
*                            *
*****************************/
/*
cd "C:\Dropbox\工作\杏容老師_work\TIGPS_w2"

**************************************************************************
est clear

use "10 data\tigps_w2.dta", clear
*/

/*****************************
*   其他相關題目(按順序)     *
*****************************/

/***************************************************************************
堅毅 Hardiness

4.請問你同不同意以下的敘述
      □(1)很不同意     □(2)不太同意    □(3)還算同意   □(4)很同意
4-1就算身體有點不舒服或有其他正當理由可以休息，我還是會努力完成每天該做的事。
4-2就算是我不喜歡的事，我也會盡全力做。
4-3就算一件事需要很長時間才會開始看到結果，我仍然會努力維持我在這件事上的表現

tab1 v4_1 v4_2 v4_3, m
***************************************************************************/
foreach x of num 1/3 {
	clonevar hardiness_`x' = v4_`x'
	mvdecode hardiness_`x', mv(-9)	
}

factor hardiness_*, pcf
estat kmo 

egen HARDINESS = rowtotal(hardiness_*), m
lab var HARDINESS "堅毅"

/***************************************************************************
家庭支持

v5.下列關於你家中生活的情形，你認為符不符合？
   □(1)很不符合     □(2)不太符合    □(3)還算符合   □(4)很符合
v5_1. 作決定時，家人會彼此商量
v5_2. 家人喜歡共度休閒時光
v5_3. 當有家庭活動時，我們家每個人都會參加
v5_4. 家人會接納彼此的朋友
v5_5. 我受到挫折時，總可以從我的家人那裡得到安慰
v5_6. 當我需要幫忙或忠告時，我可以依賴我的家人

tab1 v5_* , m
***************************************************************************/
foreach x of num 1/6 {
	clonevar famsupport_`x' = v5_`x'
	mvdecode famsupport_`x', mv(-9)
}

factor famsupport_*, pcf
estat kmo 

egen FAMSUPPORT = rowtotal(famsupport_*), m
lab var FAMSUPPORT "家庭支持"

/***************************************************************************
Life event (生活事件): 總加成為累積生活事件event12 (13題不算)

v8.上國八以來，下列事情發生過嗎？（複選題）
□1.我和好朋友感情決裂
□2.我在班級的成績、排名退步
□3.我在學校裡跟同班同學有過節
□4.我換班級
□5.被人誤會
□6.受人歧視
□7.當眾丢臉
□8.學習負擔重
□9.升學壓力
□10.我生了重病或受重傷
□11.家裡沒有錢了
□12.家中有人去世
□13.以上均無

tab1 v8*, m
***************************************************************************/
foreach x of num 1/13 {
	local tem = `x'
	if (inrange(`tem', 1, 9)) clonevar event_`x' = v8_0`x'
	else clonevar event_`x' = v8_`x'
}

mvdecode event_*, mv(-9)

egen EVENT = rowtotal(event_1 - event_12), m
replace EVENT = 0 if v8_13 == 1
lab var EVENT "累積生活事件"

/***************************************************************************
校園偏差 總加成為schdelin

10.以下對你學校的描述你覺得符不符合？ 
  (1)很不符合  (2)不太符合  (3)還算符合  (4)很符合
10-1.學生逃學或翹課情況嚴重
10-2.學生抽菸或喝酒問題嚴重

tab1 v10_*, m
***************************************************************************/
foreach x of num 1/2 {
	clonevar schdelin_`x' = v10_`x'
	mvdecode schdelin_`x', mv(-9)
}

factor schdelin_*, pcf
estat kmo 

egen SCHDELIN = rowtotal(schdelin_*), m
lab var SCHDELIN "校園偏差"

/***************************************************************************
teasupport

11.下列有關你和導師的說法，你認為符不符合目前的情況？ 
  (1)很不符合  (2)不太符合  (3)還算符合  (4)很符合
11-1. 導師很關心我
11-2. 我跟導師很親近

tab1 v11_*, m
***************************************************************************/
foreach x of num 1/2 {
	clonevar teasupport_`x' = v11_`x'
	mvdecode teasupport_`x', mv(-9)
}

factor teasupport_*, pcf
estat kmo 

egen TEASUPPORT = rowtotal(teasupport_*), m
lab var TEASUPPORT "教師支持"

/***************************************************************************
Class support

12.下列有關你們班上的說法，你認為符不符合目前的狀況？
  (1)很不符合  (2)不太符合  (3)還算符合  (4)很符合
12-1.當我需要他們的時候，我的同學們總是會適時伸出援手
12-2.當班上的同學需要我的時候，我總是會適時伸出援手
12-3.與其他班級比較，我們班的同學彼此之間，最互相信賴

tab1 v12_*, m
***************************************************************************/
foreach x of num 1/3 {
	clonevar clasupport_`x' = v12_`x'
	mvdecode clasupport_`x', mv(-9)
}

factor clasupport_*, pcf
estat kmo 

egen CLASUPPORT = rowtotal(clasupport_*), m
lab var CLASUPPORT "班級支持"

/***************************************************************************
ranking
15.你上學期的平均成績大約如何？
□(1)全班五名以內   □(2)全班六至十名   □(3)全班十一至二十名 
□(4)全班二十一至三十名               □(5)全班三十名以後

tab1 v15, m
***************************************************************************/
clonevar ranking = v15
mvdecode ranking, mv(-9)
lab var ranking "成績排名5類"

/***************************************************************************
數位壓力因應DCOP
22.當你在現實生活遇到困難感到憂慮時，你會上網：
1從未	2偶爾	3有時	4經常
22-1.找人聊天給你支持或安慰。
22-2.找人討論問題，設法解決困難。
22-3.搜尋相關的資訊，設法解決困難。
22-4.瀏覽跟困難無關的資訊，轉移自己的注意力。
22-5.玩遊戲來減少困擾或壓力感受。
22-6.購物來改善心情或壓力感受。

tab1 v22_*, m
***************************************************************************/
foreach x of num 1/6 {
	clonevar dcop_`x' = v22_`x'
	mvdecode dcop_`x', mv(-9)
}

factor dcop_*, pcf
estat kmo 

egen DCOP = rowtotal(dcop_*), m
lab var DCOP "數位壓力因應"

/***************************************************************************
網路上癮總加 = ia

28.關於上網，以下敘述符不符合你的情形？
(1)很不符合、(2)不符合、(3)符合、(4)很符合
28-1.想上網而無法上網的時候，我就會感到坐立不安。
28-2.只要有一段時間沒有上網，我就會覺得心裡不舒服。
28-3.我只要超過一天沒上網玩，就會很難受。
28-4.發現自己上網休閒的時間越來越長。
28-5.平均而言，我每周上網休閒的時間比以前增加許多。
28-6.我實際上網所花的時間遠遠超過原來預期的時間。
28-7.上網對我的學業或工作已造成一些負面的影響。
28-8.上網已經很明顯地危害到我的生理或心理健康。
28-9.因為上網的關係，我和家人及朋友的互動減少了。
28-10.我常因熬夜上網休閒，導致白天精神或體力很差。

tab1 v28_*, m
***************************************************************************/
foreach x of num 1/10 {
	clonevar ia_`x' = v28_`x'
	mvdecode ia_`x', mv(-9)
}

factor ia_*, pcf
estat kmo 

egen IA = rowtotal(ia_*), m
lab var IA "網路上癮總加"

/***************************************************************************
親社會朋友 Prosocial friend 
將分數高代表支持高 故2要反轉編碼

29.以下有關於你最要好的朋友的描述，你同不同意？
     (1)很不同意  (2)不同意  (3)同意  (4)很同意
29-1.當我需要幫助或忠告時，我可以依賴他(們)。
29-2.他(們)有時候會惹事生非。
29-3.他(們)關心我。
29-4.我常會得到他(們)的幫忙
29-5.我受到挫折時，總可以從(他)們那裡得到安慰

tab1 v29_*, m
***************************************************************************/
foreach x of num 1/5 {
	clonevar psfriend_`x' = v29_`x'
	mvdecode psfriend_`x', mv(-9)
}

replace psfriend_2 = 5 - psfriend_2

factor psfriend_*, pcf
estat kmo 

egen PSFRIEND = rowtotal(psfriend_*), m
lab var PSFRIEND "親社會朋友"

/***************************************************************************
資源近用(access2)

43.請問你有多常會遇到下面的狀況？
    (1)從未、(2)偶爾、(3)有時、(4)經常
43-1.對我來說，要取得與使用3C產品(手機、電腦、平板)的費用太高
43-2.對我來說，使用網路要付的費用太貴

tab1 v43_*, m
***************************************************************************/
foreach x of num 1/2 {
	clonevar access2_`x' = v43_`x'
	mvdecode access2_`x', mv(-9)
}

factor access2_*, pcf
estat kmo 

egen ACCESS2 = rowtotal(access2_*), m
lab var ACCESS2 "資源近用2"

/***************************************************************************
跑一下因素分析確認有多少因素
看456 媒體素養四否有在同一因素之下

還是跟其他題目會落在一起
Medialit 原則上應該要包括456 若其他題目沒有再一起 則先不加上其他題目

47.以下關於媒體網站的運用敘述，你同不同意？
          1=非常不同意;5＝非常同意
47-1.我能判斷適合找到最好答案的關鍵字
47-2.我能快速找到我曾拜訪過的網站
47-3.我很清楚自己正在觀看特定網站或使用特定app的目的
47-4.我清楚哪些訊息可以在網路上公開分享，哪些不行
47-5.我能注意上網時給出的評語是不是恰當
47-6.貼文和傳照片影片時，我會視情況設定要分享給誰看到
47-7.我能從網路找到其他人的影音，進行二次創作或改編
47-8.我能根據網頁/影音所屬的著作權規範（如創用cc）來運用找到的素材
47-9.我能將所製作的影片/網頁/圖片上傳到雲端空間

tab1 v47_*, m
***************************************************************************/
foreach x of num 1/9 {
	clonevar medialit_`x' = v47_`x'
	mvdecode medialit_`x', mv(-9)
}

factor medialit_*, pcf
estat kmo 
/*123456、789*/

egen MEDIALIT = rowtotal(medialit_1 - medialit_6), m
lab var MEDIALIT "媒體素養(1-6)"

/***************************************************************************
生活滿意度 ls
50.整體來說，你對自己的生活感到滿意嗎？
  (1)很不滿意 (2)不太滿意 (3)還算滿意 (4)很滿意
  
快樂 hp
51.整體來說，你覺得最近的日子過得快樂嗎？
  (1)很不快樂 (2)不太快樂 (3)還算快樂 (4)很快樂

自覺健康狀態 hlth
52.整體而言，你覺得自己目前的健康狀況如何？
  (1)很不健康   (2)不太健康   (3)還算健康   (4)很健康

tab1 v50 v51 v52, m
***************************************************************************/
* 生活滿意度
clonevar ls = v50
lab var ls "生活滿意度"

* 快樂
clonevar hp = v51
lab var hp "快樂"

* 自覺健康狀態
clonevar hlth = v52
lab var hlth "自覺健康狀態"

mvdecode ls hp hlth, mv(-9)


/***************************************************************************
53.請依據你過去六個月的經驗與事實，回答以下各題
      (1)不符合、(2)有些符合、(3)符合
53-1.我嘗試對別人友善，並關心他們的感受
53-2.我常與他人分享(食物、遊戲、筆等等)
53-3.如有人受傷、沮喪 或感到不適，我都樂意幫忙
53-4.我會友善地對待比我年紀小的孩子
53-5.我常自願幫助別人 (父母、老師、同學)

tab1 v53_*, m
***************************************************************************/
foreach x of num 1/5 {
	clonevar prosocial_`x' = v53_`x'
	mvdecode prosocial_`x', mv(-9)
}

factor prosocial_*, pcf
estat kmo 
alpha prosocial_*
/*
0.8866
*/

egen PROSOCIAL = rowtotal(prosocial_*), m
lab var PROSOCIAL "親社會行為"


/***************************************************************************
看一下順序跟資料是否不同，印象中codebook  這幾題，可能有調整，但我們手上的還是如此

先前只有九題 今年開始將寧願自己已經死亡的題目加回來 故CESDR10

我們跑資料的時候 分別用第一年的CESDR-9 
第二年的CESD-10 看一下
55.請勾選在過去1到2週你感受到下列選項之頻率。
   0 = 完全沒有或少於一天  1 =最近1週一到兩天  2 =最近1週三到四天 
   3 = 最近1週五到七天   4 = 最近兩週幾乎天天
55-1.我的胃口不好
55-2.我的睡眠情況不好
55-3.我覺得悲傷
55-4.我覺得自己是一個壞人
55-5.我想傷害自己 XXX
55-6.我對平時有興趣做的事情都失去興趣
55-7.我覺得自己的行動太緩慢
55-8.我總是感到疲憊
55-9.我寧願自己已經死亡 XXX
55-10.我無法集中精神做一些重要的事情
55-11.我覺得坐立不安
55-12.我覺得很寂寞(孤單、沒伴) XXX
55-13.我感到緊張焦慮 XXX
55-14.我不能停止或是無法控制擔心 XXX

tab1 v55_*, m
***************************************************************************/
foreach x of num 1/14 {
	clonevar cesdr_`x' = v55_`x'
	mvdecode cesdr_`x', mv(-9)
}

factor cesdr_*, pcf
estat kmo 

egen CESDR9 = rowtotal(cesdr_1 - cesdr_4 cesdr_6 - cesdr_8 cesdr_10 cesdr_11), m
lab var CESDR9 "憂鬱傾向9"

egen CESDR10 = rowtotal(cesdr_1 - cesdr_4 cesdr_6 - cesdr_11), m
lab var CESDR10 "憂鬱傾向10"

egen CESDR13 = rowtotal(cesdr_1 - cesdr_4 cesdr_6 - cesdr_14), m
lab var CESDR13 "憂鬱傾向12"

/***************************************************************************
情緒調節 分為CR=123 , ES=456
56.請問你同不同意以下的敘述：			
完全不同意1	2 3 4 5 6 7完全同意

56-1.我的情緒只有我自己知道
56-2.我會用想別的事情的方式，來減少自己的負面情緒(例如:難過或生氣)
56-3.當我面對有壓力的情境時，我會往可以讓自己冷靜下來的方向來想這件事
56-4.我控制自己情緒的方法是不把它們表現出來
56-5.當我想讓自己有更正向的情緒，我會換個角度來想當下的狀況
56-6.當我感受到負向情緒時，我會努力不表現出來

tab1 v56_*, m
***************************************************************************/
foreach x of num 1/3 {
	clonevar cr_`x' = v56_`x'
	mvdecode cr_`x', mv(-9)
}

foreach x of num 4/6 {
	clonevar es_`x' = v56_`x'
	mvdecode es_`x', mv(-9)
}

factor cr_*, pcf
estat kmo 
factor es_*, pcf
estat kmo 

alpha cr_*
alpha es_*
alpha cr_* es_*

egen CR = rowtotal(cr_*), m
lab var CR "情緒調節_認知評估"

egen ES = rowtotal(es_*), m
lab var ES "情緒調節_情緒壓抑"

egen CRESA = rowtotal(cr_* es_*), m
lab var CRESA "情緒調節_總分"

/***************************************************************************
WHO 5
57.請問過去兩週中，你對以下敘述的感受程度？
   0 =從來沒有  1 =有時候   2 =少於一半的時間   3 =一半以上的時間  4 =大部分的時間
57-1.我感到情緒開朗且精神不錯。 
57-2.我感到心情平靜和放鬆。 
57-3.我感到有活力且精力充沛。 
57-4.我醒來感到神清氣爽並有充分休息。 
57-5.我的日常生活中充滿讓我感興趣的事物。

tab1 v57_*, m
***************************************************************************/
foreach x of num 1/5 {
	clonevar who5_`x' = v57_`x'
	mvdecode who5_`x', mv(-9)
}

factor who5_*, pcf
estat kmo 

egen WHO5 = rowtotal(who5_*), m
lab var WHO5 "幸福感"

/***************************************************************************
個人與家庭基本資料

1.請問你的性別（生理性別）：□(1)女 □(2)男

2.請問你親生父母的婚姻狀態：
雙親 (123) 離婚(45) 未婚(67) 過世(8910)(其他若人數太少則去除)

3.我們的社會中，有一群人比較接近上層，有一群人比較接近下層。下面有一個由上到下的圖表，請問你認為自己的家庭最接近這個圖表裡面的哪一層？ 

tab1 v1 v2 kv2 v3, m
***************************************************************************/
* 女性
clonevar female = v1
recode female(1 = 1)(2 = 0)(* = .)
lab var female "女性"

*父母婚姻狀態
recode v2 (1/3 = 1 "雙親")(4 5 = 2 "離婚")(6 7 = 3 "未婚") ///
                 (8/10 = 4 "過世")(* = .), gen(pmarital)
lab var pmarital "父母婚姻狀態"
				 
* 主觀家庭階級
clonevar fclass = v3
mvdecode fclass, mv(-9)	
lab var fclass "主觀家庭階級"
