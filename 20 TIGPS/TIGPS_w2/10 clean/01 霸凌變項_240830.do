* Last Updated: 2024. 08. 30
* File name: []
* Data: tigps_w2
* Subject: 


/*****************************
*                            *
*****************************/
/*
cd "C:\Dropbox\工作\杏容老師_work\TIGPS_w2"

**************************************************************************
est clear

use "10 data\tigps_w2.dta", clear
*/

/*****************************
*   霸凌相關題目(按順序)     *
*****************************/

/***************************************************************************
v34.上八年級以來，你曾在實際生活中(非網上)，被其他人辱罵、惡意取笑、破壞或搶取東西、肢體攻擊、拒絕加入討論或活動嗎？
□(1)曾經有    □(2)不曾（跳答36）

v35.上八年級以來，你曾在實際生活中(非網上)遇到這樣的事情嗎？
(從未,偶爾,有時,經常 1,2,3,4)
v35_1.別人用髒話罵或威脅我。
v35_2.別人說我的壞話。
v35_3.不讓我加入討論、活動或不准別人跟我說話、一起行動。
v35_4.被嘲笑外表或言行舉止。
v35_5.被嘲笑我喜歡某個人。
v35_6.故意破壞我的東西或故意藏起來。
v35_7.勒索或強要我的東西。
v35_8.被某人肢體攻擊。

tab1 v34, m
tab1 v35_*, m
***************************************************************************/
* 是否實體被霸凌
recode v34 (1 = 1 "曾有實體被霸凌")(2 = 0 "不曾實體被霸凌")(* = .) ///
			, gen(br_v)
lab var br_v "曾有實體被霸凌"

* 累積實體被霸凌的頻率
foreach x of num 1/8 {
	clonevar br_fv_`x' = v35_`x'
	mvdecode br_fv_`x', mv(-999 -9 -8 -6)
	replace br_fv_`x' = br_fv_`x' - 1
}

/*
28筆
brow v34 v35_* if br_fv_1 !=. & (br_v == 0 | br_v == .)
*/
replace br_v = 0 if v35_1 == 1 & v35_2 == 1 ///
					& v35_3 == 1 & v35_4 == 1 ///
					& v35_5 == 1 & v35_6 == 1 ///
					& v35_7 == 1 & v35_8 == 1 ///
					& (br_v == 0 | br_v == .)
replace br_v = 1 if br_fv_1 != . & br_v == .

egen BR_FV = rowtotal(br_fv_1 - br_fv_8), m
lab var BR_FV "累積實體被霸凌頻率" 

foreach x of num 1/8 {
	recode br_fv_`x' (0/1 = 0)(2 = 1)(3 = 2)(* = .), gen(br_fv1_`x')
}
egen BR_FV1 = rowtotal(br_fv1_1 - br_fv1_8), m
lab var BR_FV1 "累積實體被霸凌頻率(3)"

* 累積實體被霸凌的類型
foreach x of num 1/8 {
	recode br_fv_`x' (0 = 0)(1/3 = 1), gen(br_tv_`x')
}

egen BR_TV = rowtotal(br_tv_1 - br_tv_8), m
lab var BR_TV "累積實體被霸凌類型"

/***************************************************************************
v36.上八年級以來，你曾在實際生活中(非網上)，對別人辱罵、惡意取笑、破壞或搶取東西、肢體攻擊、拒絕讓某個人加入討論或活動嗎？
□(1)曾經有    □(2)不曾（跳答38）

v37.上八年級以來，你曾在實際生活中(非網上)對別人做過這樣的事嗎？
(從未,偶爾,有時,經常 1,2,3,4)
v37_1.用髒話罵或威脅某人。
v37_2.說某人的壞話。
v37_3.不讓某人加入活動、討論事情或不准別人跟某人說話、一起行動。
v37_4.嘲笑某人的外貌或言行舉止。
v37_5.嘲笑某人他喜歡某個人。
v37_6.故意破壞某人的東西或故意藏起來。
v37_7.勒索或強要某人的東西。
v37_8.對某人肢體攻擊。

tab1 v36, m
tab1 v37_*, m
***************************************************************************/
* 是否實體霸凌他人
recode v36 (1 = 1 "曾有實體霸凌他人")(2 = 0 "不曾實體霸凌他人")(* = .) ///
			, gen(br_a)
lab var br_a "曾有實體霸凌他人"

* 累積實體霸凌他人的頻率
foreach x of num 1/8 {
	clonevar br_fa_`x' = v37_`x'
	mvdecode br_fa_`x', mv(-999 -9 -8 -6)
	replace br_fa_`x' = br_fa_`x' - 1
}

/*
3筆
brow v36 v37_* if br_fa_1 !=. & (br_a == 0 | br_a == .)
*/
replace br_a = 0 if v37_1 == 1 & v37_2 == 1 ///
					& v37_3 == 1 & v37_4 == 1 ///
					& v37_5 == 1 & v37_6 == 1 ///
					& v37_7 == 1 & v37_8 == 1 ///
					& (br_a == 0 | br_a == .)
replace br_a = 1 if br_fa_1 != . & br_a == .

egen BR_FA = rowtotal(br_fa_1 - br_fa_8), m
lab var BR_FA "累積實體霸凌他人頻率"

* 累積實體霸凌他人的類型
foreach x of num 1/8 {
	recode br_fa_`x' (0 = 0)(1/3 = 1), gen(br_ta_`x')
}

egen BR_TA = rowtotal(br_ta_1 - br_ta_8), m
lab var BR_TA "累積實體霸凌他人類型"

/***************************************************************************
v38.上八年級以來，你曾在網路上，被其他人辱罵、惡意取笑、拒絕加入討論、散布丟臉的影片、發布不實的訊息嗎？
□(1)曾經有    □(2)不曾（跳答40）


v39.上八年級以來，你曾在網路上遇到這樣的事情嗎？
(從未,偶爾,有時,經常 1,2,3,4)
v39_1.別人用髒話罵或威脅我。
v39_2.別人說我的壞話。
v39_3.不讓我加入討論。
v39_4.被嘲笑外貌、言行舉止。
v39_5.被嘲笑我喜歡某人。
v39_6.在網路散布使我丟臉的照片或影片。
v39_7.假冒我的社交帳號或是電子郵件，在網路上發布不實訊息。

tab1 v38, m
tab1 v39_*, m
***************************************************************************/
* 是否網路被霸凌
recode v38 (1 = 1 "曾有網路被霸凌")(2 = 0 "不曾網路被霸凌")(* = .) ///
			, gen(bc_v)
lab var bc_v "曾有網路被霸凌"

* 累積網路被霸凌的頻率
foreach x of num 1/7 {
	clonevar bc_fv_`x' = v39_`x'
	mvdecode bc_fv_`x', mv(-999 -9 -8 -6)
	replace bc_fv_`x' = bc_fv_`x' - 1
}

/*
6筆
brow v38 v39_* if bc_fv_1 !=. & (bc_v == 0 | bc_v == .)
*/
replace bc_v = 0 if v39_1 == 1 & v39_2 == 1 ///
					& v39_3 == 1 & v39_4 == 1 ///
					& v39_5 == 1 & v39_6 == 1 ///
					& v39_7 == 1 & (bc_v == 0 | bc_v == .)
replace bc_v = 1 if bc_fv_1 != . & bc_v == .

egen BC_FV = rowtotal(bc_fv_1 - bc_fv_7), m
lab var BC_FV "累積網路被霸凌頻率"

foreach x of num 1/7 {
	recode bc_fv_`x' (0/1 = 0)(2 = 1)(3 = 2)(* = .), gen(bc_fv1_`x')
}
egen BC_FV1 = rowtotal(bc_fv1_1 - bc_fv1_7), m
lab var BC_FV1 "累積網路被霸凌頻率(3)"

* 累積網路被霸凌的類型
foreach x of num 1/7 {
	recode bc_fv_`x' (0 = 0)(1/3 = 1), gen(bc_tv_`x')
}

egen BC_TV = rowtotal(bc_tv_1 - bc_tv_7), m
lab var BC_TV "累積網路被霸凌類型"

/***************************************************************************
v40.上八年級以來，你曾在網路上，對別人辱罵、惡意取笑、拒絕加入討論、散布丟臉的影片、發布不實訊息等事情嗎？
□(1)曾經有    □(2)不曾（跳答42）


v41.上八年級以來，你曾在網路上對別人做過這樣的事嗎？
(從未,偶爾,有時,經常 1,2,3,4)
v41_1.用髒話罵或威脅某人。
v41_2.說某人的壞話。
v41_3.不讓某人加入討論。
v41_4.嘲笑某人的外貌、言行舉止。
v41_5.嘲笑某人喜歡某個人。
v41_6.在網路散布使某人丟臉的照片或影片。
v41_7.用某人的社交帳號或是電子郵件，發布不實的訊息給其他人。

tab1 v40, m
tab1 v41_*, m
***************************************************************************/
* 是否網路霸凌他人
recode v40 (1 = 1 "曾有網路霸凌他人")(2 = 0 "不曾網路霸凌他人")(* = .) ///
			, gen(bc_a)
lab var bc_a "曾有網路霸凌他人"

* 累積網路霸凌他人的頻率
foreach x of num 1/7 {
	clonevar bc_fa_`x' = v41_`x'
	mvdecode bc_fa_`x', mv(-999 -9 -8 -6)
	replace bc_fa_`x' = bc_fa_`x' - 1
}

/*
2筆
brow v40 v41_* if bc_fa_1 !=. & (bc_a == 0 | bc_a == .)
*/
replace bc_a = 0 if v41_1 == 1 & v41_2 == 1 ///
					& v41_3 == 1 & v41_4 == 1 ///
					& v41_5 == 1 & v41_6 == 1 ///
					& v41_7 == 1 & (bc_a == 0 | bc_a == .)
replace bc_a = 1 if bc_fa_1 != . & bc_a == .

egen BC_FA = rowtotal(bc_fa_1 - bc_fa_7), m
lab var BC_FA "累積網路霸凌他人頻率"

* 累積網路霸凌他人的類型
foreach x of num 1/7 {
	recode bc_fa_`x' (0 = 0)(1/3 = 1), gen(bc_ta_`x')
}

egen BC_TA = rowtotal(bc_ta_1 - bc_ta_7), m
lab var BC_TA "累積網路霸凌他人類型"

/***************************************************************************
霸凌類別整理

A1過去只有被實體霸凌(任何一種都可以)，只有被網路霸凌，以及兩種都有的
A2只有實體霸凌別人(任何一種都可以)，只有網路霸凌別人，以及實體網路都有霸凌別人的
A3.過去有同時被霸凌或霸凌別人者
(網路與實體的變化請納入考量)             
***************************************************************************/

* 實體霸凌
gen br_4 = .
replace br_4 = 1 if br_v == 1 & br_a != 1
replace br_4 = 2 if br_v != 1 & br_a == 1
replace br_4 = 3 if br_v == 1 & br_a == 1
replace br_4 = 4 if br_v != 1 & br_a != 1
replace br_4 = . if br_v == . & br_a == .
lab def br_4 1 "只有被實體霸凌" 2 "只有實體霸凌他人" ///
                   3 "兩種實體霸凌都有" 4 "兩種實體霸凌沒都有"
lab val br_4 br_4
lab var br_4 "實體霸凌四類"
/*
tab1 br_v  br_4, m
*/

* 網路霸凌
gen bc_4 = .
replace bc_4 = 1 if bc_v == 1 & bc_a != 1
replace bc_4 = 2 if bc_v != 1 & bc_a == 1
replace bc_4 = 3 if bc_v == 1 & bc_a == 1
replace bc_4 = 4 if bc_v != 1 & bc_a != 1
replace bc_4 = . if bc_v == . & bc_a == .
lab def bc_4 1 "只有被網路霸凌" 2 "只有網路霸凌他人" ///
                   3 "兩種網路霸凌都有" 4 "兩種網路霸凌沒都有"
lab val bc_4 bc_4
lab var bc_4 "網路霸凌四類"
/*
tab1 bc_v  bc_a bc_4, m
*/
tab br_4 bc_4 , cell


* 被霸凌經驗
gen b_v = .
replace b_v = 1 if br_v == 1 & bc_v != 1
replace b_v = 2 if br_v != 1 & bc_v == 1
replace b_v = 3 if br_v == 1 & bc_v == 1
replace b_v = 4 if br_v != 1 & bc_v != 1
replace b_v = . if br_v == . & bc_v == .
lab def b_v 1 "只有被實體霸凌" 2 "只有被網路霸凌" ///
                   3 "兩種都有" 4 "兩種沒都有"
lab val b_v b_v
lab var b_v "被霸凌經驗"

* 霸凌別人經驗
gen b_a = .
replace b_a = 1 if br_a == 1 & bc_a != 1
replace b_a = 2 if br_a != 1 & bc_a == 1
replace b_a = 3 if br_a == 1 & bc_a == 1
replace b_a = 4 if br_a != 1 & bc_a != 1
replace b_a = . if br_a == . & bc_a == .
lab def b_a 1 "只有實體霸凌別人" 2 "只有網路霸凌別人" ///
                   3 "兩種都有" 4 "兩種沒都有"
lab val b_a b_a
lab var b_a "霸凌別人經驗"

* 結合 
tab b_v b_a , cell

egen b_va = count(1), by(b_v b_a)
