* Last Updated: 2024. 03. 29
* File name: []
* Data: 學生AB總檔_0131LABEL
* Subject: 


/*****************************
*                            *
*****************************/
/*
cd "C:\Dropbox\工作\杏容老師_work\tigps_w1"

***************************************************************************
est clear

use "10 data\學生AB總檔_0325LABEL.dta" ,clear
*/

/*****************************
*   其他相關題目(按順序)     *
*****************************/

/***************************************************************************
（AB卷）
請勾選在過去1到2週間您感受到下列選項之頻率。

tab1 as72a - as72l
我的胃口不好
我的睡眠情況不好
我覺得悲傷
我覺得自己是一個壞人
我對平時有興趣做的事情都失去興趣
我覺得自己的行動太緩慢
我總是感到疲憊
我無法集中精神做一些重要的事情
我覺得坐立不安
(完全沒有或少於一天,最近1週一到兩天,最近1週三到四天,
最近1週五到七天,最近兩週幾乎天天 0,1,2,3,4)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" {
	clonevar cesd_`x' = as72`x'
	mvdecode cesd_`x', mv(-999 -9)
}

factor cesd_a - cesd_i, pcf
estat kmo 

factor cesd_a - cesd_l, pcf
estat kmo 

alpha cesd_a - cesd_l

egen CESD_9 = rowtotal(cesd_a - cesd_i)
lab var CESD_9 "憂鬱傾向_9"

egen CESD_12 = rowtotal(cesd_a - cesd_l)
lab var CESD_12 "憂鬱傾向_12"

/***************************************************************************
（AB卷）
tabm as62 as63 as64

生活滿意度(ls)
整體來說，你對自己的生活感到滿意嗎？


快樂(hp)
整體來說，你覺得最近的日子過得快樂嗎？

自覺健康狀態(hlth) 
整體而言，你覺得自己目前的健康狀況如何？

(選項1：很不滿意、選項2：不太滿意、選項3：還算滿意、選項4：很滿意)
***************************************************************************/

* 生活滿意度
clonevar ls = as62
lab var ls "生活滿意度"

* 快樂
clonevar hp = as63
lab var hp "快樂"

* 自覺健康狀態
clonevar hlth = as64
lab var hlth "自覺健康狀態"

mvdecode ls hp hlth, mv(-999 -9)

/***************************************************************************
（AB卷）
tabm as65a as65b as65c
個人自尊(SESTEEM)
下列是有關你對自己的描述，你同不同意？

我是個有價值（有用）的人
我用積極樂觀的態度看待我自己
我很滿意我自己
(很不同意,不太同意,還算同意,很同意	1,2,3,4)
***************************************************************************/
foreach x in "a" "b" "c" {
	clonevar sesteem_`x' = as65`x'
	mvdecode sesteem_`x', mv(-999 -9)	
}

factor sesteem_a - sesteem_c, pcf
estat kmo 


*egen SESTEEM = rmean(sesteem_a - sesteem_c)
egen SESTEEM = rowtotal(sesteem_a - sesteem_c), m
lab var SESTEEM "個人自尊"

/***************************************************************************
（AB卷）
家庭支持(FS)
tabm as9*
下列關於你家中生活的情形，你認為符不符合？
作決定時，家人會彼此商量
家人喜歡共度休閒時光
當有家庭活動時，我們家每個人都會參加
家人會接納彼此的朋友
我受到挫折時，總可以從我的家人那裡得到安慰
當我需要幫忙或忠告時，我可以依賴我的家人
(很不符合,不太符合,還算符合,很符合	1,2,3,4)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e" "f" {
	clonevar fs_`x' = as9`x'
	mvdecode fs_`x', mv(-999 -9)	
}

factor fs_a - fs_f, pcf
estat kmo 

*egen FS = rmean(fs_a - fs_f)
egen FS = rowtotal(fs_a - fs_f), m
lab var FS "家庭支持"

/***************************************************************************
（AB卷）
學校支持(SCH)
(school_02 反向)
tabm as14*
下列是有關學校的描述，你同不同意？
我喜歡學校。
我不認為我是學校的一份子。
我以我的學校為榮。
(1很不同意 2不太同意 3還算同意 4很同意)
***************************************************************************/
foreach x in "a" "b" "c" {
	clonevar sch_`x' = as14`x'
	mvdecode sch_`x', mv(-999 -9)
}

replace sch_b = 5 - sch_b

factor sch_a - sch_c, pcf
estat kmo 

*egen SCH = rmean(sch_a - sch_c)
egen SCH = rowtotal(sch_a - sch_c), m
lab var SCH "學校支持"

/***************************************************************************
（AB卷）
班級支持(CLASS) 
(class_02 反向)
tabm as16*

下列有關你們班上的說法，你認為符不符合目前的狀況？
當我需要他們的時候，我的同學們總是會適時伸出援手
我不喜歡與班上同學交往
當班上的同學需要我的時候，我總是會適時伸出援手
我們班的同學相親相愛，猶如一家人
與其他班級比較，我們班的同學彼此之間，最互相信賴
((1)很不符合  (2)不太符合  (3)還算符合  (4)很符合)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e" {
	clonevar class_`x' = as16`x'
	mvdecode class_`x', mv(-999 -9)	
}

replace class_b = 5 - class_b

factor class_a - class_e, pcf
estat kmo 

*egen CLASS = rmean(class_a - class_e)
egen CLASS = rowtotal(class_a - class_e), m
lab var CLASS "班級支持"

/***************************************************************************
（AB卷）
朋友支持(FRIEND)
tabm as42*
以下有關於你最要好的朋友的描述，你同不同意？
當我需要幫助或忠告時，我可以依賴他(們)。
他(們)有時候會惹事生非。
他(們)關心我。
我常會得到他(們)的幫忙
我受到挫折時，總可以從(他)們那裡得到安慰
(1很不同意 2不同意 3同意 4很同意)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e"  {
	clonevar friend_`x' = as42`x'
	mvdecode friend_`x', mv(-999 -9)	
}

factor friend_a - friend_e, pcf
estat kmo 

egen FRIEND = rowtotal(friend_a - friend_e), m
lab var FRIEND "朋友支持"

/***************************************************************************
（AB卷）
網路上癮(IADD)
關於上網，以下敘述符不符合您的情形？
tabm as41*
想上網而無法上網的時候，我就會感到坐立不安。
只要有一段時間沒有上網，我就會覺得心裡不舒服。
我只要超過一天沒上網玩，就會很難受。
發現自己上網休閒的時間越來越長。
平均而言，我每周上網休閒的時間比以前增加許多。
我實際上網所花的時間遠遠超過原來預期的時間。
上網對我的學業或工作已造成一些負面的影響。
上網已經很明顯地危害到我的生理或心理健康。
因為上網的關係，我和家人及朋友的互動減少了。
我常因熬夜上網休閒，導致白天精神或體力很差。
(很不符合,不太符合,還算符合,很符合	1,2,3,4)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" {
	clonevar iadd_`x' = as41`x'
	mvdecode iadd_`x', mv(-999 -9)
}

factor iadd_a - iadd_j, pcf
estat kmo 

egen IADD = rmean(iadd_a - iadd_j)
lab var IADD "網路上癮"

/***************************************************************************
(A卷
數位資源進用(DRE)
請問你有多常會遇到下面的狀況？
tabm as57*
對我的家庭來說，要購買3C產品(手機、電腦、平板)的費用太高
我住的地方沒有網路訊號或是訊號太弱
對我的家庭來說，使用網路要付的費用太貴
在我住的地方，很難找到一個不受干擾的空間來上網
((1)從未、(2)偶爾、(3)有時、(4)經常)
***************************************************************************/
foreach x in "a" "b" "c" "d"  {
	clonevar dre_`x' = as57`x'
	mvdecode dre_`x', mv(-999 -9)
}

factor dre_a - dre_d, pcf
estat kmo 

egen DRE = rmean(dre_a - dre_d)
lab var DRE "數位資源進用"

egen DRE_sum = rowtotal(dre_a - dre_d)
lab var DRE_sum "數位資源進用(sum)"

/***************************************************************************
（A卷）
數位壓力因應(DCOP)
tabm as37*
當你在現實生活遇到困難感到憂慮時，你會上網：
找人聊天給你支持或安慰
找人討論問題，設法解決困難
搜尋相關的資訊，設法解決困難
瀏覽跟困難無關的資訊，轉移自己的注意力
玩遊戲來減少困擾或壓力感受
購物來改善心情或壓力感受
(1=從未、 2=偶爾、3=有時、4=經常)
***************************************************************************/
foreach x in "a" "b" "c" "d" "e" "f" {
	clonevar dcop_`x' = as37`x'
	mvdecode dcop_`x', mv(-999 -9)
}

factor dcop_a - dcop_f, pcf
estat kmo 

egen DCOP_f1 = rmean(dcop_a - dcop_c)
lab var DCOP_f1 "數位壓力因應f1"

egen DCOP_f2 = rmean(dcop_d - dcop_f)
lab var DCOP_f2 "數位壓力因應f2"

/*
egen DCOP = rmean(dcop_a - dcop_f)
lab var DCOP "數位壓力因應"

egen DCOP_sum = rowtotal(dcop_a - dcop_f),m
lab var DCOP_sum "數位壓力因應(sum)"
*/

/***************************************************************************
（AB卷）
課業進度(schwork)
tab as19,m
你跟得上學校課業進度嗎？()
□(1)我落後很多，很難跟得上	□(2)我有點落後，可能跟得上
□(3)只落後一點點，很快就跟上了	□(4)大部分都跟得上    □(5)我的進度超前

***************************************************************************/
clonevar schwork = as19
mvdecode schwork, mv(-999 -9)	
lab var schwork "跟上課業進度"

/***************************************************************************
（AB卷）
偏差(DElNQ
注意答14不納入計算
tabm as55_1 - as55_14

45.過去6個月以來，請問你有沒有做過下列的事情？（複選題）
□1考試作弊 □2離家出走 □3蹺課或逃學 □4偷拿別人的東西
□5故意破壞別人的東西 □6蓄意打傷別人 □7抽紙菸（香菸）
□8抽電子菸 □9喝酒 □10嚼檳榔 □11收聽或觀看盜版的音樂與影片
□12下載非法軟體 □13深夜不回家在外遊蕩  □14都沒有
***************************************************************************/
foreach x of num 1/14 {
	clonevar DElNQ_`x' = as55_`x'
	mvdecode DElNQ_`x', mv(-999 -9)
}


egen DElNQ = rowtotal(DElNQ_1 - DElNQ_13)
replace DElNQ = 0 if DElNQ_14 == 1

lab var DElNQ "偏差行為(個數)"

/***************************************************************************
（AB卷）
tab1 as21,m
父母學業要求 (parsch) 
家長對你的學業成績有些甚麼要求？
1.至少班上前幾名   2.至少中上   3. 要有班上的平均水準
4.沒有特別要求     5. 其他(請說明)              
***************************************************************************/
/*
*/

/***************************************************************************
（AB卷）
成績排名
***************************************************************************/
recode as20 (1 = 1 "全班五名以內")(2 = 2 "全班六至十名") ///
            (3 = 3 "全班十一至二十名")(4 = 4 "全班二十一至三十名") ///
			(5 = 5 "全班三十名以後")(-7 = 6 "不知道")(* = .) ///
			,gen(rank)
lab var rank "成績排名"

/***************************************************************************
（AB卷）
個人背景:

性別(女性))
tab1 as1, m
請問你的性別（生理性別）

家庭經濟狀況(feco)
tab1 as10, m
請問你認為家裡的經濟狀況為何？
***************************************************************************/
* 女性
clonevar female = as1
recode female(1 = 1)(2 = 0)(* = .)
lab var female "女性"

* 家庭經濟狀況
clonevar feco = as10
mvdecode feco, mv(-999 -9)	
lab var feco "自評家庭經濟狀況"

/***************************************************************************
（AB卷）
同住家人
***************************************************************************/
gen cohabit = .
replace cohabit = 3 if as8_11 == 1 | as8_12 == 1 | as8_13 == 1 | as8_14 == 1
replace cohabit = 2 if (as8_1 == 1 & as8_2 != 1 & as8_4 != 1) ///
                     | (as8_2 == 1 & as8_1 != 1 & as8_3 != 1) ///
                     | (as8_3 == 1 & as8_2 != 1 & as8_4 != 1) ///
                     | (as8_4 == 1 & as8_1 != 1 & as8_3 != 1) 
replace cohabit = 1 if (as8_1 == 1 & as8_2 == 1) | (as8_3 == 1 & as8_4 == 1) ///
                     | (as8_1 == 1 & as8_4 == 1) | (as8_2 == 1 & as8_3 == 1)
replace cohabit = 4 if cohabit == .

lab var cohabit "家庭結構"
lab def cohabit 1 "雙親" 2 "單親" 3 "祖父母" 4 "其他"
lab val cohabit cohabit


